import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "$kotlinVersion" apply false
  id "org.jetbrains.kotlin.plugin.serialization" version "$kotlinPluginSerializationVersion" apply false
  id "org.jlleitschuh.gradle.ktlint" version "$ktlintVersion" apply false
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // Maven coordinates need group + version
  // Change this to your real group (GitHub org is a common choice)
  group = "io.github.canopyengine"
  version = "$projectVersion"

  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
}

configure(subprojects) {
  apply plugin: 'java-library'
  apply plugin: 'kotlin'
  apply plugin: 'maven-publish'

  java.sourceCompatibility = 8

  // Produce -sources.jar (recommended for IDE users)
  java {
    withSourcesJar()
    // withJavadocJar() // optional; Kotlin projects often skip unless you generate docs
  }

  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    File assetsFolder = new File("${project.rootDir}/assets/")
    File assetsFile = new File(assetsFolder, "assets.txt")
    assetsFile.delete()

    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  processResources.dependsOn 'generateAssetList'

  compileJava {
    options.incremental = true
  }
  compileKotlin.compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)
  compileTestKotlin.compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)

  // ---- Publishing to ~/.m2 ----
  publishing {
    publications {
      // Publishes: <group>:<artifactId>:<version>
      mavenJava(MavenPublication) {
        // This publishes the normal jar from the java component
        from components.java

        // Default artifactId is project.name; keep or customize:
        artifactId = project.name

        // Optional: fill some minimal POM metadata (safe to keep small for now)
        pom {
          name = project.name
          description = "Canopy module: ${project.path}"
        }
      }
    }
    repositories {
      // publishToMavenLocal / publishAllPublicationsToMavenLocal
      mavenLocal()
    }
  }
}

subprojects {
  ext.appName = 'canopy'
  repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
  }
}

eclipse.project.name = 'canopy' + '-parent'
